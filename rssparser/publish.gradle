apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.4.4'
group 'com.prof.rssparser'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/rssparser-release.aar")  {
                builtBy tasks.getByName("assembleRelease")
            }
            groupId
            artifactId 'rssparser'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'RSS-Parser'
        description = "An Android library to parse RSS Feed"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/prof18/RSS-Parser.git'
        version {
            name = this.version
            desc = "Example ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}